buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.github.spotbugs.snom:spotbugs-gradle-plugin:6.0.26'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'checkstyle'
}

repositories { mavenCentral() }

ext {
    junitVersion = '5.9.2'
    jacocoVersion = '0.8.12'
    spotbugsToolVersion = '4.9.8'
    checkstyleVersion = '10.3.4'
}

dependencies {
}

testing {
    suites {
        test(JvmTestSuite) {
            useJUnitJupiter()

            dependencies {
                implementation project.dependencies.create("org.junit.jupiter:junit-jupiter-api:5.9.2")
                implementation project.dependencies.create("org.junit.jupiter:junit-jupiter-params:5.9.2")
                runtimeOnly   project.dependencies.create("org.junit.jupiter:junit-jupiter-engine:5.9.2")
            }
        }
    }
}

application {
    mainClass.set('Main')
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = "full"
        showStandardStreams = true
    }
}

jacoco {
    toolVersion = jacocoVersion
}
test.finalizedBy jacocoTestReport

if (project.hasProperty('staticAnalysis')) {
    apply plugin: 'com.github.spotbugs'

    spotbugs {
        ignoreFailures = true
        showProgress = true
    }

    // Configure SpotBugs tasks to generate HTML reports
    tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
        reports {
            html {
                required = true
                outputLocation = file("$buildDir/reports/spotbugs/${name}.html")
                stylesheet = 'fancy-hist.xsl'
            }
            xml {
                required = false
            }
        }
    }
}

// Checkstyle configuration (always available)
checkstyle {
    toolVersion = checkstyleVersion
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
}
